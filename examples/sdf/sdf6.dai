fn donut(x, y, z) {
    var radius = 0.4;
    var thickness = 0.3;
    var xy_d = math.sqrt(x*x + y*y) - radius;
    var d = math.sqrt(xy_d*xy_d + z*z);
    return d - thickness / 2;
}

fn sample(x, y) {
    var z = -10;
    for (var _, _step in range(30)) {
        var θ = time.timef() * 2;
        var t_x = x * math.cos(θ) - z * math.sin(θ);
        var t_z = x * math.sin(θ) + z * math.cos(θ);
        var d = donut(t_x, y, t_z);
        if (d <= 0.01) {
            return '#';
        } else {
            z += d;
        }
    }
    return ' ';
}

while (true) {
    var frame_chars = [];
    for (var _, y in range(20)) {
        for (var _, x in range(80)) {
            var remapped_x = x / 80.0 * 2 - 1;
            var remapped_y = (y / 20.0 * 2 - 1) * (2 * 20.0 / 80);
            frame_chars.append(sample(remapped_x, remapped_y));
        }
        frame_chars.append('\n');
    }
    print('\x1b[2J' + ''.join(frame_chars));
    time.sleep(1.0 / 30);
}
