var b = 0;
var step = 1;


fn draw(canvas) {
    // canvas.clear() 清空 canvas 的内容
    canvas.clear();
    for (var r in range(256)) {
        for (var g in range(256)) {
            // canvas.setFillColor(r, g, b) 设置填充颜色
            // canvas.setFillColor(r, g, b, a) 设置填充颜色
            canvas.setFillColor(r, g, b);
            // canvas.fillRect(x, y, w, h) 填充矩形
            canvas.fillRect(r * 4, g * 4, 4, 4);
        }
    }
    b += step;
    if (b >= 255) {
        step = -1;
    }
    elif (b <= 0) {
        step = 1;
    }
    var m = [];
}


fn main() {
    print("Canvas example");
    con canvas = Canvas.new(600, 600);

    // canvas.addEventListener(event_type, callback) 添加事件监听器
    // event_type 事件类型
    //      EventKeyDown 键盘按下事件
    //      EventKeyUp 键盘抬起事件
    //      EventMouseDown 鼠标按下事件
    //      EventMouseUp 鼠标抬起事件
    //      EventMouseMotion 鼠标移动事件
    // callback 回调函数接受一个参数 event
    canvas.addEventListener(Canvas.EventKeyDown, fn(event) {
            // 键盘事件 KeyBoardEvent 包括下列字段
            //   code int 键码
            //   key str 字符串键值
            //   altKey bool 是否按下 Alt 键
            //   ctrlKey bool 是否按下 Ctrl 键
            //   shiftKey bool 是否按下 Shift 键
            //   metaKey bool 是否按下 command/windows 键
            //   repeat bool 是否重复按下
            print(event, "Key pressed:", event.code, event.key);
            print(
                "    Alt:",
                event.altKey,
                "Ctrl:",
                event.ctrlKey,
                "Shift:",
                event.shiftKey,
                "Meta:",
                event.metaKey,
            );
            // Keycode 枚举值可以看 canvas_enum.dai
            if (event.code == Canvas.Keycode.key_a) {
                print("    A pressed");
            }
            elif (event.code == Canvas.Keycode.key_escape) {
                print("    Escape pressed");
                // 调用 canvas.quit() 退出 canvas
                canvas.quit();
            }
        });
    canvas.addEventListener(Canvas.EventMouseDown, fn(event) {
            // 鼠标事件 MouseEvent 包括下列字段
            //   x float 鼠标在 canvas 上的 x 坐标
            //   y float 鼠标在 canvas 上的 y 坐标
            //   button canvas.MouseButton 鼠标按键
            if (event.button == Canvas.MouseButton.left) {
                print(event, "Left mouse pressed:", event.x, event.y);
            }
            elif (event.button == Canvas.MouseButton.middle) {
                print(event, "Middle mouse pressed:", event.x, event.y);
            }
            elif (event.button == Canvas.MouseButton.right) {
                print(event, "Right mouse pressed:", event.x, event.y);
            }
        });
    // canvas.addEventListener(Canvas.EventMouseMotion, fn(event){
    //     print(event, "Mouse move:", event.x, event.y, event.button);
    // });

    // canvas.run(ms, callback) 接收一个无参函数作为参数
    // 在窗口关闭或者调用 canvas.quit() 后返回
    canvas.run(1000 / 30, draw);
    print("Canvas quited");
}

main();
